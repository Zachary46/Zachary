apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId "com.zlf.demo"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled = true
    }
    /**
     * gradle3.0修改apk包名
     */
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                //这里修改apk文件名
                outputFileName = "${variant.name}-v${variant.versionName}.apk"
                //判断是release还是debug版本
                def buildType = variant.buildType.name
                //获取当前时间的"YYYY-MM-dd"格式。
                def createTime = new Date().format("YYYY-MM-dd", TimeZone.getTimeZone("GMT+08:00"))
                //如果是正式包,将其输入到指定文件夹
                if (buildType == "release") {
                    variant.getPackageApplication().outputDirectory = new File('/Users/zachary/zlf'+ "/${variant.productFlavors[0].applicationId}/${createTime}")
                }
            }
    }
    signingConfigs {
        release {
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASSWORD
            storeFile file(KEY_PATH)
            storePassword KEY_PASSWORD
            v1SigningEnabled true
            v2SigningEnabled true
        }
        debug {
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASSWORD
            storeFile file(KEY_PATH)
            storePassword KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFile 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFile 'proguard-rules.pro'
        }
    }

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        incremental = false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    flavorDimensions "zachary"

    productFlavors {

        dev {
            dimension "zachary"
            applicationId "com.zlf.demo"
            versionCode 10
            versionName "1.0"
            manifestPlaceholders = [BAIDU_KEY: "RRKdx3Svk27UIIWFN7eMfAX3dk41KCPb"]
            buildConfigField("String", "REQUEST_URL", "\"http://192.168.10.74:9079/huntsman-mobile-web/\"")

        }
        rel {
            dimension "zachary"
            applicationId "com.zlf.demo"
            versionCode 10
            versionName "1.0"
            manifestPlaceholders = [BAIDU_KEY: "RRKdx3Svk27UIIWFN7eMfAX3dk41KCPb"]
            buildConfigField("String", "REQUEST_URL", "\"http://app.huntsman.ssit-xm.com.cn/huntsman-mobile-web/\"")

        }
        bata {
            dimension "zachary"
            applicationId "com.zlf.demo"
            versionCode 10
            versionName "1.0"
            manifestPlaceholders = [BAIDU_KEY: "RRKdx3Svk27UIIWFN7eMfAX3dk41KCPb"]
            buildConfigField("String", "REQUEST_URL", "\"http://119.29.117.160:61022/huntsman-mobile-web/\"")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':lib:loadsir')

    implementation rootProject.ext.dependencies.libSupportV7
    implementation rootProject.ext.dependencies.libSupportDesign
    implementation rootProject.ext.dependencies.libRxBinding
    implementation rootProject.ext.dependencies.libGlide
    implementation rootProject.ext.dependencies.libRxPermissions
    implementation rootProject.ext.dependencies.libRxjava2
    implementation rootProject.ext.dependencies.libRetrofit2
    implementation rootProject.ext.dependencies.libRxandroid
    implementation rootProject.ext.dependencies.libGsonConvert
    implementation rootProject.ext.dependencies.libRxRetrofitAdapter
    implementation rootProject.ext.dependencies.libLogger
    implementation rootProject.ext.dependencies.libConstraint
    implementation rootProject.ext.dependencies.libBottomBar
    implementation rootProject.ext.dependencies.libStatusBar
    implementation rootProject.ext.dependencies.libBaseAdapter
    implementation rootProject.ext.dependencies.libUtils
}
